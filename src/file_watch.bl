Watcher :: struct {
	using win32;
	handle: HANDLE;
	path:   string;
	flags:  WatchFor;
	remove: bool;
}

watch_init :: fn () {
	using std;
	mutex_init(&worker_context.mutex);
	async(&job, &worker_context, &worker);
}

watch_terminate :: fn () {
	using std;
	worker_context.cancel = true;
	async_wait(&job);
	mutex_terminate(&worker_context.mutex);
}

WatchFor :: enum u32 #flags {
	FILE_CHANGES      = win32.FILE_NOTIFY_CHANGE_FILE_NAME;
	DIRECTORY_CHANGES = win32.FILE_NOTIFY_CHANGE_DIR_NAME;
	MODIFICATIONS     = win32.FILE_NOTIFY_CHANGE_LAST_WRITE;
}

watch_changes :: fn (directory: string_view, flags: WatchFor) Error {
	using win32;
	using std;

	hash :: hash_combine(str_hash(directory), cast(u32) flags);

	mutex_lock(&worker_context.mutex);
	defer mutex_unlock(&worker_context.mutex);

	tbl :: &worker_context.watched;
	w := tbl_lookup_ptr(tbl, hash);
	if w {
		w.remove = false;
		return OK;
	}

	handle :: FindFirstChangeNotificationA(strtoc(directory), TRUE, auto flags);
	if handle == INVALID_HANDLE || handle == null {
		return os_get_last_error2();
	}

	w = tbl_insert(tbl, hash);
	w.path   = str_new(directory);
	w.handle = handle;
	w.remove = false;
	w.flags  = flags;

	return OK;
}

unwatch_changes :: fn (directory: string_view, flags: WatchFor) {
	using win32;
	using std;

	hash :: hash_combine(str_hash(directory), cast(u32) flags);

	mutex_lock(&worker_context.mutex);
	defer mutex_unlock(&worker_context.mutex);

	tbl :: &worker_context.watched;
	w :: tbl_lookup_ptr(tbl, hash);
	if !w { return; } // Maybe report this?
	str_delete(&w.path);
	assert(w);
	w.remove= true;
}

next_watcher_event :: fn (changed_directory: *string_view, change_kind: *WatchFor) bool {
	using std;
	mutex_lock(&worker_context.mutex);
	defer mutex_unlock(&worker_context.mutex);

	changed :: &worker_context.changed;

	dt :: frame_delta_time;
	loop i := 0; i < changed.len; i += 1 {
		c :: &changed.values[i];
		c.timeout_ms -= dt;
		if c.timeout_ms < 0.f {
			@changed_directory = tprint(c.path);
			@change_kind       = c.flags;
			tbl_erase(changed, changed.keys[i]);
			return true;
		}
	}

	return false;
}

get_file_last_write_time :: fn (filepath: string_view) (t: u64, err: Error) {
	using win32;

	attributes: WIN32_FILE_ATTRIBUTE_DATA;
	if GetFileAttributesExA(std.strtoc(filepath), GET_FILEEX_INFO_LEVELS.GetFileExInfoStandard, auto &attributes) == TRUE {
		tmp: union {
			wt: FILETIME;
			t: u64;
		};
		tmp.wt = attributes.ftLastWriteTime;
		return tmp.t, OK;
	}

	return 0, os_get_last_error2();
}

#private

// Note that the file-watch system on Windows is SUPER SHITTY, there is no suitable way to watch changes to
// individual files, and notification system for directories might generate multiple events for a "single" user
// action e.g. save. Also even we have the save notification, the file still might be blocked by writing process,
// so we have timeout to give Windows some spare time to do its shit.

Change :: struct {
	path:       string;
	flags:      WatchFor;
	timeout_ms: f32;
}

job: std.Async(*typeof(worker_context), s32);

worker_context: struct {
	using std;

	watched: Table(u32, Watcher);
	changed: Table(string_view, Change);
	mutex: Mutex;

	cancel: bool;
}

worker :: fn (ctx: *typeof(worker_context)) s32 {
	using win32;
	using std;

	print_log("File watch started.");

	handles:   [..]HANDLE;
	hashes:    [..]u32;
	to_remove: [..]u32;
	defer array_terminate(&handles);
	defer array_terminate(&hashes);
	defer array_terminate(&to_remove);
	defer tbl_terminate(&ctx.watched);
	defer tbl_terminate(&ctx.changed);
	tbl :: &ctx.watched;

	loop !ctx.cancel {
		{
			mutex_lock(&worker_context.mutex);
			defer mutex_unlock(&worker_context.mutex);

			handles.len   = 0;
			hashes.len    = 0;
			to_remove.len = 0;
			loop i := 0; i < tbl.len; i += 1 {
				w :: &tbl.values[i];
				if w.remove {
					FindCloseChangeNotification(w.handle);
					str_delete(&w.path);
					array_push(&to_remove, tbl.keys[i]);
					continue;
				}

				array_push(&handles, w.handle);
				array_push(&hashes,  tbl.keys[i]);
			}
			loop i := 0; i < to_remove.len; i += 1 {
				tbl_erase(tbl, to_remove[i]);
			}
			assert(handles.len == hashes.len);
		}

		if handles.len == 0 {
			if !ctx.cancel { os_sleep_ms(100); }
			continue;
		}
		status :: WaitForMultipleObjects(auto handles.len, handles.ptr, FALSE, 100);
		if status == WAIT_TIMEOUT { continue; }

		{
			mutex_lock(&worker_context.mutex);
			defer mutex_unlock(&worker_context.mutex);

			if status >= 0 && status < auto hashes.len {
				index :: cast(s64) status;
				hash  :: hashes[index];
				w :: tbl_lookup_ptr(tbl, hash);
				assert(w);
				assert(w.handle == handles[index]);

				c := tbl_lookup_ptr(&ctx.changed, w.path);
				if !c {
					c = tbl_insert(&ctx.changed, w.path);
					c.path  = w.path;
					c.flags = auto 0;
				}
				c.flags |= w.flags;
				c.timeout_ms = 200.f;

				if FindNextChangeNotification(w.handle) == FALSE {
					print_err(os_get_last_error2());
					w.remove = true;
				}
			}
		}
		temporary_reset(); // just in case...
	}

	return 0;
}
